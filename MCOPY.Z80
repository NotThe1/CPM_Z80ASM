; MCOPY.Z80


; Amendment Record
; *****************
; Name          Date            Details of Amendment
; ----          ----            ---------------------
; Frank Martyn  2019-12-06     Version 1.1 Small buffer coded & tested
; Frank Martyn  2019-12-03     Version 1 Full working  iteration
; Frank Martyn  2019-11-13     Incorporated MAC_SC, Macros for System Calls


;               ==============
;               External Files
;               ==============

            .XLIST
            include   MAC-SC.LIB
            include   MCOPY.LIB
            .LIST

;               =============
;               Local Equates
;               =============

WBOOT       EQU       0000h                  ; system reboot

FCB1        EQU       005ch                  ; first file name
FCBsrc      EQU       FCB1                   ; source fcb
FCB2        EQU       006ch                  ; second file name
TPA         EQU       0100h                  ; Transient Program Area Start
stackSize   EQU       20H                    ; depth of stack
FALSE       EQU       0                      ; False
TRUE        EQU       -1                     ; True
;**************************************************************************
            ORG       TPA

START:
            LD        SP,stack
            CALL      SetupFiles             ; set up Files
            CALL      SetParams              ; determine buffer size & Records
ReadLoop0:
            CALL      InitParams             ; init pointers and counts
ReadLoop:
            SeqRd     FCBsrc                 ; read a record
            OR        A
            JP        NZ,WriteStart0         ; jump if at end-of-file
            CALL      UpdReadParams
            JR        NZ,ReadLoop            ; loop if still room in buffer
            LD        HL,(BuffRecMax)
            LD        (availRecs),HL         ; reset available records
            JR        WriteStart
WriteStart0:

            CALL      UpdReadParams


            LD        A,TRUE
            LD        (EOF),A                ; set EOF to true
WriteStart:
            LD        HL,workBuff            ; set buffer pointer to beginning
            LD        (nextRecLoc),HL
            SetDMA    workBuff               ; reset DMA to start of the buffer

WriteLoop:
            SeqWr     FCBdst                 ; write record
            OR        A
            JP        NZ,DiskFull            ; jump if Disk is full
            CALL      updWriteParams         ; update the pointers etc
            JR        NZ,WriteLoop           ; loop if still data in buffer
            LD        A,(EOF)
            CP        TRUE                   ; are we at end of file ?
            JP        NZ,ReadLoop0           ;  ; do more if not

; finished
            FileCls   FCBsrc
            FileCls   FCBdst
            LD        DE,$Done
            JP        Exit
            JP        WBOOT                  ; exit, via warm boot


; read the whole file
FileEOF:
            LD        DE,$FileEOF
            JP        Exit
; disk was full
DiskFull:
            LD        DE,$DiskFull
            JP        Exit

SetParams:
            LD        HL,(0006)              ; get BDOS Start
            LD        DE,workBuff            ; get start of work buffer
            LD        (nextRecLoc),DE        ; establish start of buffer
            OR        A                      ; reset CY flag
            SBC       HL,DE                  ; working buffer size
            LD        (buffBytMax),HL        ; save size of buffer
            SHIFTHL_R 7                      ; divide by Record size (080H)
            LD        (BuffRecMax),HL        ; save the number of records

; clear the buffer
            LD        HL,workBuff
            LD        (HL),00H
            LD        DE, workBuff+1
            LD        BC,(buffBytMax)
            DEC       BC
            LDIR
            RET

InitParams:
            LD        HL,(BuffRecMax)
            LD        (availRecs),HL         ; reset available records
            SetDMA    workBuff               ; init the DMA
            LD        DE,0000H
            LD        (recsRead),DE          ; initialize read record count
            LD        DE,workBuff            ; get start of work buffer
            LD        (nextRecLoc),DE        ; establish start of buffer
            RET

SetupFiles:
            CopyMem   FCB2,FCBdst,010H       ; setup destination FCB

            XOR       A                      ;
            LD        (curRec),A             ; Set current rec = 0
; source and destination  FCBs ready
            FileOpen  FCBsrc                 ; Open Source File
            LD        DE,$NoSrc              ; ready message
            INC       A                      ; 255 becomes 0
            CALL      Z, Exit                ; done if no file
; source file open, prep destination
            FileDel   FCBdst                 ; Delete Destination
            FileMak   FCBdst                 ; Create Destination
            LD        DE,$NoDirSpc           ; ready message
            INC       A                      ; 255 becomes 0
            CALL      Z, Exit                ; done if no dir space
            RET

UpdateDMA:
; Update current buffer position
            LD        HL,(nextRecLoc)        ; get current position
            LD        DE,@RecSize            ; get CPM record Size
            ADD       HL,DE                  ; add to get target position
            LD        (nextRecLoc),HL        ; update the pointer
; Set DMA to match buffer position
            EX        DE,HL                  ; prepare for sys call
            LD        C,@SetDMA              ; Sys call number
            CALL      @BDOS                  ; Make the system call
            RET

; will return ZFlag set if buffer exhausted
UpdReadParams:
            CALL      UpdateDMA              ; adjust buffer pointer & DMA
; Keep count of the records read
            LD        HL,(recsRead)          ; keep count of
            INC       HL                     ;  of the actual number
            LD        (recsRead),HL          ;  of records read
; Check if there is still room in the buffer
            LD        HL,(availRecs)         ; how many records left in buffer
            DEC       HL
            LD        (availRecs),HL         ; save for later
            LD        A,L
            CP        0
            RET       NZ                     ; exit if L != 0
            ADD       H                      ; L= 0 if H = 0, ZFlag set
            RET                              ; return (check ZFlag)

; will return ZFlag set if buffer exhausted
updWriteParams:
            CALL      UpdateDMA              ; adjust buffer pointer & DMA
; Keep count of the records read
            LD        HL,(recsRead)          ; keep count of
            DEC       HL                     ;  of the actual number
            LD        (recsRead),HL          ;  of records written
            LD        A,L
            CP        0
            RET       NZ                     ; exit if L != 0
            ADD       H                      ; L= 0 if H = 0, ZFlag set
            RET                              ; return (check ZFlag)


; write message given in DE, then reboot
Exit:
            PrtStr
            JP        WBOOT                  ; exit, via warm boot

;               =============
;                 Constants
;               =============
; console messages
$NoSrc:     DB        'No source file$'
$NoDirSpc:  DB        'No directory space$'
$DiskFull:  DB        'Out of data space$'
$WrtProt:   DB        'Write protected?$'
$Done:      DB        'Copy complete$'
$FileEOF:   DB        'File EOF$'
$FullBuff:  DB        'Buffer Full$'

;               =============
;                   Data
;               =============
FCBdst:
            DS        32                     ; destination fcb
curRec:                                      ; FCB current record

buffBytMax: DS        2                      ; current buffer Record size
BuffRecMax: DS        2                      ; current buffer Record size
availRecs:  DS        2                      ; current buffer available in Buffer
nextRecLoc: DS        2                      ; Location for next record
recsRead    DS        2                      ; actual number of records read
EOF:        DB        FALSE                  ; End of File
label1:
            DS        stackSize              ; 16 level Stack
            ORG       (($/0100H)+1) * 0100H

stack:                                       ; top of Stack

workBuff:                                    ; start of working buffer

            END       START
