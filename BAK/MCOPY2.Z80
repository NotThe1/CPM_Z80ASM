; MCOPY2.Z80


; Amendment Record
; *****************
; Name          Date            Details of Amendment
; ----          ----            ---------------------
; Frank Martyn  2019-12-03     Version 1 Full working  iteration
; Frank Martyn  2019-11-13     Incorporated MAC_SC, Macros for System Calls


;               ==============
;               External Files
;               ==============

            .XLIST
            include   MAC-SC.LIB
            include   MCOPY.LIB
            .LIST

;               =============
;               Local Equates
;               =============

WBOOT       EQU       0000h                  ; system reboot

FCB1        EQU       005ch                  ; first file name
FCBsrc      EQU       FCB1                   ; source fcb
FCB2        EQU       006ch                  ; second file name
TPA         EQU       0100h                  ; Transient Program Area Start
stackSize   EQU       20H                    ; depth of stack

;**************************************************************************
            ORG       TPA

START:
            LD        SP,Stack
            CALL      setParams              ; set up buff and record counts
            CALL      setupFiles             ; set up Files

ReadLoop:
            SeqRd     FCBsrc                 ; read a record
            OR        A
            JP        NZ,WriteStart          ; jump if at end-of-file
            CALL      updReadParams
            JR        NZ,ReadLoop            ; loop if still room in buffer
            LD        HL,(BuffRecMax)
            LD        (AvailRecs),HL         ; reset available records
WriteStart:
            LD        HL,(RecsRead)
            DEC       HL
            LD        (RecsRead),HL          ; adjust for actual records read
            LD        HL,workBuff            ; set buffer pointer to beginning
            LD        (NextRecLoc),HL
            SetDMA    workBuff               ; reset DMA to start of the buffer

WriteLoop:
            SeqWr     FCBdst                 ; write record
            OR        A
            JP        NZ,DiskFull            ; jump if Disk is full
            CALL      updWriteParams         ; update the pointers etc
            JR        NZ,WriteLoop           ; loop if still data in buffer

; finished
            FileCls   FCBsrc
            FileCls   FCBdst
            LD        DE,$Done
            JP        exit
            JP        WBOOT                  ; exit, via warm boot


; read the whole file
FileEOF:                                     ;
            LD        DE,$FileEOF
            JP        exit
; disk was full
DiskFull:
            LD        DE,$DiskFull
            JP        exit

setParams:
            LD        HL,(0006)              ; get BDOS Start
            LD        DE,WorkBuff            ; get start of work buffer
            LD        (NextRecLoc),DE        ; establish start of buffer
            OR        A                      ; reset CY flag
            SBC       HL,DE                  ; working buffer size
            SHIFTHL_R 7                      ; divide by Record size (080H)

            LD        (BuffRecMax),HL        ; save the number of records
            LD        (AvailRecs),HL         ; save available number of records

            SetDMA    WorkBuff               ; init the DMA

            LD        DE,0000H
            LD        (RecsRead),DE          ; initialize read record count

            RET

setupFiles:
            CopyMem   FCB2,FCBdst,010H       ; setup destination FCB

            XOR       A                      ;
            LD        (CurRec),A             ; Set current rec = 0
; source and destination  FCBs ready
            FileOpen  FCBsrc                 ; Open Source File

            LD        DE,$NoSrc              ; ready message
            INC       A                      ; 255 becomes 0
            CALL      Z, exit                ; done if no file
; source file open, prep destination
            FileDel   FCBdst                 ; Delete Destination

            FileMak   FCBdst                 ; Create Destination

            LD        DE,$NoDirSpc           ; ready message
            INC       A                      ; 255 becomes 0
            CALL      Z, exit                ; done if no dir space
            RET


updateDMA:
; Update current buffer position
            LD        HL,(NextRecLoc)        ; get current position
            LD        DE,@RecSize            ; get CPM record Size
            ADD       HL,DE                  ; add to get target position
            LD        (NextRecLoc),HL        ; update the pointer
; Set DMA to match buffer position
            EX        DE,HL                  ; prepare for sys call
            LD        C,@SetDMA              ; Sys call number
            CALL      @BDOS                  ; Make the system call
            RET

; will return ZFlag set if buffer exhausted
updReadParams:
            CALL      updateDMA              ; adjust buffer pointer & DMA
; Keep count of the records read
            LD        HL,(RecsRead)          ; keep count of
            INC       HL                     ;  of the actual number
            LD        (RecsRead),HL          ;  of records read
; Check if there is still room in the buffer
            LD        HL,(AvailRecs)         ; how many records left in buffer
            DEC       HL
            LD        (AvailRecs),HL         ; save for later
            LD        A,L
            CP        0
            RET       NZ                     ; exit if L != 0
            ADD       H                      ; L= 0 if H = 0, ZFlag set
            RET                              ; return (check ZFlag)

; will return ZFlag set if buffer exhausted
updWriteParams:
            CALL      updateDMA              ; adjust buffer pointer & DMA
; Keep count of the records read
            LD        HL,(RecsRead)          ; keep count of
            DEC       HL                     ;  of the actual number
            LD        (RecsRead),HL          ;  of records written
            LD        A,L
            CP        0
            RET       NZ                     ; exit if L != 0
            ADD       H                      ; L= 0 if H = 0, ZFlag set
            RET                              ; return (check ZFlag)


; write message given in DE, then reboot
exit:
            PrtStr
            JP        WBOOT                  ; exit, via warm boot

;               =============
;                 Constants
;               =============
; console messages
$NoSrc:     DB        'no source file$'
$NoDirSpc:  DB        'no directory space$'
$DiskFull:  DB        'out of data space$'
$WrtProt:   DB        'Write protected?$'
$Done:      DB        'Copy complete$'
$FileEOF:   DB        'File EOF$'
$FullBuff:  DB        'Buffer Full'

;               =============
;                   Data
;               =============
FCBdst:
            DS        32                     ; destination fcb
CurRec:

BuffBytMax: DS        2                      ; current buffer Record size
BuffRecMax: DS        2                      ; current buffer Record size
AvailRecs:  DS        2                      ; current buffer available in Buffer
NextRecLoc: DS        2                      ; Location for next record
RecsRead    DS        2                      ; actual number of records read

label1:
            DS        stackSize              ; 16 level Stack
            ORG       (($/0100H)+1) * 0100H

Stack:                                       ; top of Stack

WorkBuff:                                    ; start of working buffer

            END       START
